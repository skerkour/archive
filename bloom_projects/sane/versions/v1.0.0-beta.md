# v1.0.0 (beta)


## Table of content

* [Example](#example)
* [Spec](#spec)
* [Filename extension](#filename-extension)
* [MIME type](#mime-type)
* [ABNF](#abnf)
* [Key/Value pair](#keyvalue-pair)
* [Keys](#keys)
* [Comment](#comment)
* [String](#string)
* [Integer](#integer)
* [Float](#float)
* [Boolean](#boolean)
* [List](#list)
* [Map](#map)


---------------------


## Example

```sane
# This is a SANE document

title = "SANE Example"

creator = {
  name = "Sylvain Kerkour",
  website = "https://kerkour.com",
}

database = {
  server = "192.168.1.1",
  ports = [ 8001, 8001, 8002 ],
  connection_max = 5000,
  enabled = true,
}

servers = {
  # Indentation (only spaces) is allowed but not required
  alpha = {
    ip = "10.0.0.1",
    dc = "eqdc10",
  },

  beta = {
    ip = "10.0.0.2",
    dc = "eqdc10",
  },
}

empty_map = {}

hosts = [
  "alpha",
  "omega",
]
```



## Spec

* SANE is case sensitive.
* A SANE file must be a valid UTF-8 encoded Unicode document.
* Whitespace means space (0x20).
* Newline means LF (0x0A).
* Values can have any length. However, parsers MUST produce an error for strings that are too long
  for them to safely handle (String, Map or List too long).



## Filename extension

SANE files should use the extension `.sane`.



## MIME type

When transferring SANE files over the internet, the appropriate MIME type is `application/sane`.



# ABNF

```abnf
;; This is an attempt to define SANE in ABNF according to the grammar defined
;; in RFC 5234 (http://www.ietf.org/rfc/rfc5234.txt).

;; Overall Structure

sane = expression *( newline expression )

expression =  ws [ comment ]
expression =/ ws keyval ws [ comment ]
expression =/ ws map ws [ comment ]

;; Whitespace

ws = *%x20

;; Newline

newline =  %x0A ; LF

;; Comment

comment-start-symbol = %x23 ; #
non-eol =  %x09
non-eol =/ %x20-10FFFF

comment = comment-start-symbol *non-eol

;; Key-Value pairs

keyval = key keyval-sep val

key = simple-key / dotted-key
simple-key = quoted-key / unquoted-key

unquoted-key = 1*( ALPHA / DIGIT / %x2D / %x5F ) ; A-Z / a-z / 0-9 / - / _
quoted-key = basic-string / literal-string
dotted-key = simple-key 1*( dot-sep simple-key )

dot-sep   = ws %x2E ws  ; . Period
keyval-sep = ws %x3D ws ; =

val = string / boolean / list / map / float / integer

;; String

string = ml-basic-string / basic-string / ml-literal-string / literal-string

;; Basic String

basic-string = quotation-mark *basic-char quotation-mark

quotation-mark = %x22            ; "

basic-char = basic-unescaped / escaped
basic-unescaped = %x20-21 / %x23-5B / %x5D-7E / %x80-10FFFF
escaped = escape escape-seq-char

escape = %x5C                   ; \
escape-seq-char =  %x22         ; "    quotation mark  U+0022
escape-seq-char =/ %x5C         ; \    reverse solidus U+005C
escape-seq-char =/ %x62         ; b    backspace       U+0008
escape-seq-char =/ %x66         ; f    form feed       U+000C
escape-seq-char =/ %x6E         ; n    line feed       U+000A
escape-seq-char =/ %x72         ; r    carriage return U+000D
escape-seq-char =/ %x74         ; t    tab             U+0009
escape-seq-char =/ %x75 4HEXDIG ; uXXXX                U+XXXX
escape-seq-char =/ %x55 8HEXDIG ; UXXXXXXXX            U+XXXXXXXX

;; Multiline Basic String

ml-basic-string = ml-basic-string-delim ml-basic-body ml-basic-string-delim

ml-basic-string-delim = 3quotation-mark

ml-basic-body = *( ml-basic-char / newline / ( escape ws newline ) )
ml-basic-char = ml-basic-unescaped / escaped
ml-basic-unescaped = %x20-5B / %x5D-7E / %x80-10FFFF

;; Literal String

literal-string = apostrophe *literal-char apostrophe

apostrophe = %x27 ; ' apostrophe

literal-char = %x09 / %x20-26 / %x28-7E / %x80-10FFFF

;; Multiline Literal String

ml-literal-string = ml-literal-string-delim ml-literal-body ml-literal-string-delim

ml-literal-string-delim = 3apostrophe

ml-literal-body = *( ml-literal-char / newline )
ml-literal-char = %x09 / %x20-7E / %x80-10FFFF

;; Integer

integer = dec-int / hex-int / oct-int / bin-int

minus = %x2D                       ; -
plus = %x2B                        ; +
underscore = %x5F                  ; _
digit1-9 = %x31-39                 ; 1-9
digit0-7 = %x30-37                 ; 0-7
digit0-1 = %x30-31                 ; 0-1

hex-prefix = %x30.78               ; 0x
oct-prefix = %x30.6f               ; 0o
bin-prefix = %x30.62               ; 0b

dec-int = [ minus / plus ] unsigned-dec-int
unsigned-dec-int = DIGIT / digit1-9 1*( DIGIT / underscore DIGIT )

hex-int = hex-prefix HEXDIG *( HEXDIG / underscore HEXDIG )
oct-int = oct-prefix digit0-7 *( digit0-7 / underscore digit0-7 )
bin-int = bin-prefix digit0-1 *( digit0-1 / underscore digit0-1 )

;; Float

float = float-int-part ( exp / frac [ exp ] )
float =/ special-float

float-int-part = dec-int
frac = decimal-point zero-prefixable-int
decimal-point = %x2E               ; .
zero-prefixable-int = DIGIT *( DIGIT / underscore DIGIT )

exp = "e" float-int-part

special-float = [ minus / plus ] ( inf / nan )
inf = %x69.6e.66  ; inf
nan = %x6e.61.6e  ; nan

;; Boolean

boolean = true / false

true    = %x74.72.75.65     ; true
false   = %x66.61.6C.73.65  ; false

;; List

list = list-open [ list-values ] ws-comment-newline list-close

list-open =  %x5B ; [
list-close = %x5D ; ]

list-values =  ws-comment-newline val ws list-sep list-values
list-values =/ ws-comment-newline val ws [ list-sep ]

list-sep = %x2C  ; , Comma

ws-comment-newline = *( ws / [ comment ] newline )

;; Map

map = map-open [ map-keyvals ] ws-comment-newline map-close

map-open  = %x7B ws     ; {
map-close = ws %x7D     ; }
map-sep   = ws %x2C ws  ; , Comma

map-pair = key keyval-sep val

map-keyvals = ws-comment-newline map-pair ws map-sep map-keyvals
map-keyvals =/ ws-comment-newline map-pair ws [ map-sep ]

;; Built-in ABNF terms, reproduced here for clarity

ALPHA = %x41-5A / %x61-7A ; A-Z / a-z
DIGIT = %x30-39 ; 0-9
HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
```


## Key/Value pair

The primary building block of a SANE document is the key/value pair.

Keys are on the left of the equals sign and values are on the right. Whitespaces are ignored around
key names and values. The key, equals sign, and value must be on the same line (though some values
can be broken over multiple lines).

Key/value pairs are not guaranteed to be in any specific order.

```sane
key = "value"
```

Values must be of the following types: String, Integer, Float, Boolean, List, or Map. Unspecified
values are invalid.

```sane
key = # INVALID
"value" # INVALID
```





## Keys

A key may be either bare or quoted.

**Bare keys** may only contain ASCII letters, ASCII digits, underscores, and dashes (`A-Za-z0-9_-`).
Note that bare keys are allowed to be composed of only ASCII digits, e.g. 1234, but are always interpreted as strings.

```sane
key = "value"
bare_key = "value"
bare-key = "value"
1234 = "value"
```

**Quoted keys** follow the exact same rules as either basic strings or literal strings and allow you
to use a much broader set of key names. Best practice is to use bare keys except when absolutely necessary.

```sane
"127.0.0.1" = "value"
"character encoding" = "value"
"ʎǝʞ" = "value"
'key2' = "value"
'quoted "value"' = "value"
```

A *bare key* **must not be empty**.

```sane
= "no key name"  # INVALID
"" = "blank"     # INVALID
'' = 'blank'     # INVALID
```

**Duplicate keys is invalid**
```sane
# DO NOT DO THIS
name = "Sylvain"
name = "Kerkour"

map = {
  name = "Sylvain",
  name = "Kerkour",
}
```



## Comment

One of the specificity of SANE is that comments are first class citizen in order to allow programs to
write comments. Thus lexers MUST lex comments and parsers implement a way to retrieve the document metadata (TODO).

A hash symbol (`#`) marks the rest of the line as a comment unless it's inside a quoted string.

A comment should be parsed with it's starting hash symbol.

```sane
# This is a full-line comment -> Token{Comment, "# This is a full-line comment"}
key = "value" # This is a comment at the end of a line

#This is a comment -> Token{Comment, "#This is a comment"}
#   This is another comment -> Token{Comment, "#  This is another comment"}
```




## String


There are four ways to express strings: **basic**, **multi-line basic**, **literal**, and
**multi-line literal**. All strings MUST contain only valid UTF-8 characters.

**Basic strings** are surrounded by quotation marks. Any Unicode character may be used except those
that must be escaped: quotation mark, backslash, and the control characters (U+0000 to U+001F, U+007F).

```sane
str = "I'm a string. \"You can quote me\". Name\tJos\u00E9\nLocation\tSF."
```

For convenience, some popular characters have a compact escape sequence.
```
\b         - backspace       (U+0008)
\t         - tab             (U+0009)
\n         - linefeed        (U+000A)
\f         - form feed       (U+000C)
\r         - carriage return (U+000D)
\"         - quote           (U+0022)
\\         - backslash       (U+005C)
\uXXXX     - unicode         (U+XXXX)
\UXXXXXXXX - unicode         (U+XXXXXXXX)
```

Any Unicode character may be escaped with the `\uXXXX` or `\UXXXXXXXX` forms. The escape codes must
be valid Unicode
<a href="http://unicode.org/glossary/#unicode_scalar_value" target="_blank" rel="noopener noreferrer">scalar values</a>.

All other escape sequences not listed above are reserved and, if used, SANE should produce an error.

Sometimes you need to express passages of text (e.g. translation files) or would like to break up a
very long string into multiple lines. SANE makes this easy.

**Multi-line basic strings** are surrounded by three quotation marks on each side and allow newlines.
A newline immediately following the opening delimiter will be trimmed. All other whitespace and newline
characters remain intact.

```sane
# the following strings are the same

str1 = """
Roses are red
Violets are blue"""

str2 = """Roses are red
Violets are blue"""
```

SANE parsers should feel free to normalize newline to whatever makes sense for their platform.


```sane
# On a Unix system, the above multi-line string will most likely be the same as:
str2 = "Roses are red\nViolets are blue"

# On a Windows system, it will most likely be equivalent to:
str3 = "Roses are red\r\nViolets are blue"
```

For writing long strings without introducing extraneous whitespace, use a "line ending backslash".
When the last non-whitespace character on a line is a `\`, it will be trimmed along with all whitespace
(including newlines) up to the next non-whitespace character or closing delimiter.
All of the escape sequences that are valid for basic strings are also valid for multi-line basic strings.


```sane
# The following strings are byte-for-byte equivalent:
str1 = "The quick brown fox jumps over the lazy dog."

str2 = """
The quick brown \


  fox jumps over \
    the lazy dog."""

str3 = """\
       The quick brown \
       fox jumps over \
       the lazy dog.\
       """
```

Any Unicode character may be used except those that must be escaped: backslash and the control
characters (U+0000 to U+001F, U+007F). Quotation marks need not be escaped unless their presence
would create a premature closing delimiter.

If you're a frequent specifier of Windows paths or regular expressions, then having to escape
backslashes quickly becomes tedious and error prone. To help, SANE supports **literal strings** which
do not allow escaping at all.

**Literal strings** are surrounded by single quotes. Like basic strings, they must appear on a single line:

```sane
# What you see is what you get.
winpath  = 'C:\Users\nodejs\templates'
winpath2 = '\\ServerX\admin$\system32\'
quoted   = 'Sylvain "z0mbie42" Kerkour'
regex    = '<\i\c*\s*>'
```

Since there is no escaping, there is no way to write a single quote inside a literal string enclosed
by single quotes. Luckily, SANE supports a multi-line version of literal strings that solves this problem.

**Multi-line literal strings** are surrounded by three single quotes on each side and allow newlines.
Like literal strings, there is no escaping whatsoever. A newline immediately following the opening
delimiter will be trimmed. All other content between the delimiters is interpreted as-is without modification.

```sane
regex2 = '''I [dw]on't need \d{2} apples'''
lines  = '''
The first newline is
trimmed in raw strings.
   All other whitespace
   is preserved.
'''
```

Control characters other than tab are not permitted in a literal string. Thus, for binary data it is
recommended that you use Base64 or another suitable ASCII or UTF-8 encoding.
The handling of that encoding will be application specific.






## Integer

**Integers** are whole numbers. Positive numbers may be prefixed with a plus sign. Negative numbers
are prefixed with a minus sign.

Parsers MUST produce an error if the value cannot be represented in an internal type.
Parsers MUST NOT produce an integer that does not equal the full value represented in SANE.

```sane
int1 = +99
int2 = 42
int3 = 0
int4 = -17
```

For large numbers, you may use underscores between digits to enhance readability.
Each underscore MUST be surrounded by at least one digit on each side.

```sane
int5 = 1_000
int6 = 5_349_221
int7 = 1_2_3_4_5 # VALID but discouraged
```

Leading zeros are not allowed. Integer values `-0` and `+0` are valid and identical to an
unprefixed zero.

Non-negative integer values may also be expressed in hexadecimal, octal, or binary.
In these formats, leading zeros are allowed (after the prefix). Hex values are case insensitive.
Underscores are allowed between digits (but not between the prefix and the value).

```sane
# hexadecimal with prefix `0x`
hex1 = 0xDEADBEEF
hex2 = 0xdeadbeef
hex3 = 0xdead_beef

# octal with prefix `0o`
oct1 = 0o01234567
oct2 = 0o755 # useful for Unix file permissions

# binary with prefix `0b`
bin1 = 0b11010110
```

64 bit (signed long) range expected (`−9_223_372_036_854_775_808` to `9_223_372_036_854_775_807`).





## Float

**Floats** MUST be implemented as IEEE 754 binary64 values.

A float consists of an integer part (which follows the same rules as integer values) followed by a
fractional part and/or an exponent part. If both a fractional part and exponent part are present,
the fractional part MUST precede the exponent part.

```sane
# fractional
flt1 = +1.0
flt2 = 3.1415
flt3 = -0.01

# exponent
flt4 = 5e+22
flt5 = 1e6
flt6 = -2E-2 # INVALID, e MUST BE lowercase

# both
flt7 = 6.626e-34

flt8 = 3. # INVALID fractional part is missing
```

A fractional part is a decimal point followed by one or more digits.

An exponent part is an `e` (lower case only) followed by an integer part (which follows the same
rules as integer values).

Similar to integers, you may use underscores to enhance readability. Each underscore must be surrounded
by at least one digit.

```sane
flt8 = 9_224_617.445_991_228_313
```

Float values `-0.0` and `+0.0` are valid and should map according to **IEEE 754**.

Special float values can also be expressed. They are always lowercase.

```sane
# infinity
sf1 = inf  # positive infinity
sf2 = +inf # positive infinity
sf3 = -inf # negative infinity

# not a number, must be lowercase, valid but not recommended
sf4 = nan  # actual sNaN/qNaN encoding is implementation specific
sf5 = +nan # same as `nan`
sf6 = -nan # valid, actual encoding is implementation specific
```





## Boolean

**Booleans** are just the tokens you're used to. Always lowercase.

```sane
bool1 = true
bool2 = false
bool3 = False // INVALID
```






## List

**Lists** are ordered collections of values. Whitespace is ignored. Elements are separated by commas.
Data types MUST NOT be mixed (different ways to define strings should be considered the same type).

```sane
arr1 = [ 1, 2, 3 ]
arr2 = [ "red", "yellow", "green" ]
arr3 = [ [ 1, 2 ], [3, 4, 5] ]
arr4 = [ "all", 'strings', """are the same""", '''type''']
arr5 = [ { x = 1, y = "2" }, { v = false } ] # VALID, list<map>
arr6 = [] # VALID, type will depend of the data it is deserialized to
arr7 = [[]] # VALID, type will depend of the data it is deserialized to
arr8 = [[],[1]] # VALID, list<list<Integer>>

arr9 = [ [ 1, 2 ], ["a", "b", "c"] ] # INVALID, deep heterogeneity is forbidden
arr10 = [ 1, 2.0 ] # INVALID, shallow heterogeneity is forbidden
```

Lists can also be multiline. Terminating commas (also called trailing commas) are ok after the
last value of the list. There can be an arbitrary number of newlines and comments before a value and
before the closing bracket.

```sane
arr7 = [
  1, 2, 3
]

arr8 = [
  1,
  2, # this is ok
]
```




## Map

**Maps** (also known as hash tables or dictionaries) are collections of key/value pairs.
As for Lists, whitespaces are ignored. Elements are separated by commas.

```sane
empty_map = {}
```

Naming rules for maps are the same as for keys (see definition of Keys above).
Key/value pairs within maps are not guaranteed to be in any specific order.

```sane
dog = {
  "tater.man" = { 5 = 0x5 }
}
```

In JSON land, that would give you the following structure:

```json
{
  "dog": {
    "tater.man": {
      "5": 5
    }
  }
}
```

Duplicate Keys within a map is INVALID.

```sane
map = { 123 = 1, 123 = 2 } # INVALID
```


As for Lists, Maps can also be multiline. Terminating commas (also called trailing commas) are ok after the last value of the map. There can be an arbitrary number of newlines and comments before a value and before the closing brace.

```sane
map = {
  a = 1,
  b = 2, # trailing comma is ok
}
```
